# SConstruct file for Shield
#
# This is the equivalent of a Makefile, but for use with the SCons
# build system
#
# Author: Axel Liljencrantz
# Copyright: FreeCode AS
# License: GPLv3

# Construct the main environment
#
# Add the base build directory to cdpath. That way, #includes can all
# prepend the directory in which they are located, and everything will
# work just spiffy!

env = Environment(CPPPATH=[Dir('.')])
Export('env')

import glob
import os
import sys
from stat import *

# Respect various defaults from the environment
for name in ['CXX','CC','CFLAGS','CXXFLAGS']:
	if os.getenv(name) != None:
		env[name] = os.getenv(name)

# Implementation of the 'OracleCheck' custom check
def CheckOracleInclude(context, locs):
	'''Check for a suitable oracle include directory
	'''	
	for loc in locs:
		path = os.path.join(loc,'public')
		if os.path.isdir(path):
			context.env.Append(CPPPATH=[Dir(path)])
			return True	

def CheckOracleLink(context, locs):
	'''Check for a suitable oracle link directory
	'''	
	for loc in locs:
		path = os.path.join(loc,'lib')
		if os.path.isdir(path):
			context.env.Append(LIBPATH=[Dir(path)])
			return True	
		
def CheckOracle(context):
	'''Check for presense of needed Oracle libraries. If Oracle can be found, the include and link paths are modified to incorporate oracle and True is returned. Otherwise False is returned.
	'''	
	context.Message('Checking for Oracle installation files')
	locs_wc = ['/usr/lib/oracle/xe/app/oracle/product/*/server/rdbms', '/usr/lib/oracle/xe/app/oracle/product/*/client/rdbms', '/usr/lib/oracle/xe/app/oracle/product/*/server', '/usr/lib/oracle/xe/app/oracle/product/*/client']
	locs=[]
	for loc in locs_wc:
		locs = locs+glob.glob(loc)

	result=True	
	if not CheckOracleInclude(context, locs):
		result=False
	if not CheckOracleLink(context, locs):
		result=False
	context.Result(result)
	return result

def CheckGxx(context):
	'''Check if the c++ compiler is g++. Return True if yes, false otherwise.
	'''
	context.Message('Checking if the compiler is g++')
	result = context.env['CXX'][0:3] == 'g++'
	context.Result(result)
	return result

conf = Configure(env, custom_tests = {'CheckOracle' : CheckOracle, 'CheckGxx' : CheckGxx})

#
# Check for presence of various headers
#
for header in ['getopt.h', 'sys/select.h']:
	if conf.CheckCHeader(header):
		var_name = header.upper().replace('.','/').replace('/','_')
		conf.env['CXXFLAGS']+=' -DHAVE_'+var_name+'=1'
	
#
# Check for presence of getopt.h header
#
if conf.CheckFunc('getopt_long'):
	conf.env['CXXFLAGS']+= ' -DHAVE_GETOPT_LONG=1'
	

#
# Call the CheckOracle custom check
#
if not conf.CheckOracle():
	print 'Oracle OCCI library files are not installed!'
	print 'They can be found in e.g. the Oracle-XE client libraries,'
	print 'which can be downloaded from http://www.oracle.com.'
	Exit(1)

# 
# Add gcc-specifig options
#
if conf.CheckGxx():
	conf.env['CXXFLAGS']+=' -pipe -Woverloaded-virtual -Wnon-virtual-dtor -Wformat -Wimplicit-int -Wimplicit-function-declaration -Wmissing-braces -Wunused -Wreturn-type'
	conf.env['LINKFLAGS']+=' -rdynamic'

# Suggested warnings from Claes Næstén
#
#-fsyntax-only  -pedantic  -pedantic-errors -w  -Wextra  -Wall
#           -Waddress  -Waggregate-return -Wno-attributes -Wc++-compat
#           -Wcast-align  -Wcast-qual  -Wchar-subscripts  -Wcomment -Wconver‐
#           sion  -Wno-deprecated-declarations -Wdisabled-optimization
#           -Wno-div-by-zero  -Wno-endif-labels -Werror  -Werror=* -Wer‐
#           ror-implicit-function-declaration -Wfatal-errors  -Wfloat-equal
#           -Wformat  -Wformat=2 -Wno-format-extra-args -Wformat-nonliteral
#           -Wformat-security  -Wformat-y2k -Wimplicit  -Wimplicit-func‐
#           tion-declaration  -Wimplicit-int -Wimport  -Wno-import  -Winit-self
#           -Winline -Wno-int-to-pointer-cast -Wno-invalid-offsetof  -Win‐
#           valid-pch -Wlarger-than-len  -Wunsafe-loop-optimizations
#           -Wlong-long -Wmain  -Wmissing-braces  -Wmissing-field-initializers
#           -Wmissing-format-attribute  -Wmissing-include-dirs -Wmissing-nore‐
#           turn -Wno-multichar  -Wnonnull  -Wno-overflow -Woverlength-strings
#           -Wpacked  -Wpadded -Wparentheses  -Wpointer-arith
#           -Wno-pointer-to-int-cast -Wredundant-decls -Wreturn-type  -Wse‐
#           quence-point  -Wshadow -Wsign-compare  -Wstack-protector
#           -Wstrict-aliasing -Wstrict-aliasing=2 -Wstrict-overflow
#           -Wstrict-overflow=n -Wswitch  -Wswitch-default  -Wswitch-enum
#           -Wsystem-headers  -Wtrigraphs  -Wundef  -Wuninitialized -Wun‐
#           known-pragmas  -Wno-pragmas -Wunreachable-code -Wunused
#           -Wunused-function  -Wunused-label  -Wunused-parameter
#           -Wunused-value  -Wunused-variable  -Wvariadic-macros
#           -Wvolatile-register-var  -Wwrite-strings


env = conf.Finish()

#
# Build the src subdirectory with a separate build dirctory called 'build'
#
SConscript('src/SConscript', build_dir='build')

# Build the actual programs
SConscript('bin/SConscript')

